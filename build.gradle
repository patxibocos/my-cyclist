buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.android.plugin.gradle
        classpath libs.kotlin.plugin.gradle
        classpath libs.hilt.plugin.gradle
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.14.2'
}

subprojects {
    configurations.configureEach {
        // We forcefully exclude AppCompat + MDC from any transitive dependencies.
        // This is a Compose app, so there's no need for these
        exclude group: 'androidx.appcompat'
        exclude group: 'com.google.android.material', module: 'material'
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(libs.versions.ktlint.get())
        }
    }
}